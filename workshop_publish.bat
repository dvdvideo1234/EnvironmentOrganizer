:: Hard reset the HEAD: git push origin +<NEW_HEAD_COMMIT_HASH>:master
:: 6af74344a888bdf9fbb35d887c3f77691820a50e
:: git push origin +6af74344a888bdf9fbb35d887c3f77691820a50e:master

@echo off

:: Remove the ID in case of creating an addon. Fill it to update
set AddonMyWorkshopID=771977116

:: Parameterization variables
set AddonGameBasePath=F:\Games\Steam\steamapps\common\GarrysMod\bin
set AddonWorkshopName=EnvironmentOrganizer
set AddonDirsSelected=(lua)

:: Automated from this point down
set AddonRepositoryPath=%~dp0
set /A AddonValidationID=%AddonMyWorkshopID%+0

title Addon %AddonWorkshopName% updater/publisher

echo Press Crtl+C to terminate !
echo Press a key if you do not want to wait !
echo Rinning in %AddonRepositoryPath%.
echo Npp Find --\h{1,}\n-- replace --\n-- in dos format before commit !
echo.

timeout 3
rd /S /Q %AddonRepositoryPath%Workshop
md %AddonRepositoryPath%Workshop\%AddonWorkshopName%
for %%i in %AddonDirsSelected% do (
  echo Extracting %%i
  timeout 3
  xcopy %AddonRepositoryPath%%%i %AddonRepositoryPath%Workshop\%AddonWorkshopName%\%%i /EXCLUDE:%AddonRepositoryPath%data\workshop\key.txt /E /C /I /F /R /Y
)

copy %AddonRepositoryPath%data\workshop\addon.json %AddonRepositoryPath%Workshop\%AddonWorkshopName%\addon.json
call %AddonGameBasePath%\gmad.exe create -folder "%AddonRepositoryPath%Workshop\%AddonWorkshopName%" -out "%AddonRepositoryPath%Workshop\%AddonWorkshopName%.gma"

if defined AddonMyWorkshopID (
  if "%AddonValidationID%"=="0" (
    echo "The workshop addon ID <%AddonMyWorkshopID%> is invalid !"
    goto addonError
  )
  echo "Updating: %AddonWorkshopName% under ID <%AddonMyWorkshopID%>"
  call %AddonGameBasePath%\gmpublish.exe update -addon "%AddonRepositoryPath%Workshop\%AddonWorkshopName%.gma" -id "%AddonMyWorkshopID%" -icon "%AddonRepositoryPath%data\pictures\icon.jpg" -changes "Generated by Batch!"
) else (
  echo "Publishing: %AddonWorkshopName%"
  call %AddonGameBasePath%\gmpublish.exe create -addon "%AddonRepositoryPath%Workshop\%AddonWorkshopName%.gma" -icon "%AddonRepositoryPath%data\pictures\icon.jpg"
)

:addonError

echo.

timeout 500
